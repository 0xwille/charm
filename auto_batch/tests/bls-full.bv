name := bls
# number of signatures in a batch
N := 100

# description of key generation and signing algorithms
BEGIN :: func:keygen
input := None
 g := random(G2)
 x := random(ZR)
 pk := g^x
 sk := x
output := list{pk, sk}
END :: func:keygen

BEGIN :: func:sign
input := list{sk, M}
 sig := (H(M, G1))^sk
output := sig
END :: func:sign

# types for variables used in verification.
# all other variable types are inferred by SDL parser
BEGIN :: types
h := G1
sig := G1
g := G2
pk := G2
M := str
END :: types

# variables computed before each signature verification
BEGIN :: precompute
  h := H(M, G1)
END :: precompute

constant := g
public :=  pk
signature :=  sig
message :=  h

# single signer
BEGIN :: setting
message := N
public := one
signature := N
END :: setting

# individual verification check
verify := {e(h, pk) == e(sig, g)}