name := bls
# number of signatures in a batch
N := 100
setting := asymmetric

# types for variables used in verification.
# all other variable types are inferred by SDL parser
BEGIN :: types
h := G1
sig := G1
g := G2
pk := G2
M := str
END :: types

# description of key generation and signing algorithms
BEGIN :: func:keygen
input := None
 g := random(G2)
 x := random(ZR)
 pk := g^x
 sk := x
output := list{pk, sk}
END :: func:keygen

BEGIN :: func:sign
input := list{sk, M}
 sig := (H(M, G1))^sk
output := sig
END :: func:sign

BEGIN :: func:verify
 input := list{pk, M, sig}
 h := H(M, G1)
 verify := {e(h, pk) == e(sig, g)}
 output := verify 
END :: func:verify

constant := g
public :=  pk
signature :=  sig
message :=  h

# single signer
BEGIN :: setting
message_count := N
public_count := one
signature_count := N
END :: setting

# variables computed before each signature verification
BEGIN :: precompute
  h := H(M, G1)
END :: precompute

# individual verification check
verify := {e(h, pk) == e(sig, g)}